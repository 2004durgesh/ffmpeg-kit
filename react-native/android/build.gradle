buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:8.1.0'
  }
}

apply plugin: 'com.android.library'

static def safePackageName(String packageName) {
  packageName.replace("-lts", "")
}

def safePackageVersion(String packageName) {
  def version = project.properties['ffmpegKit.android.main.version']
  def ltsVersion = project.properties['ffmpegKit.android.lts.version']
  packageName.contains("-lts") ? ltsVersion + ".LTS" : version
}

def safeExtGet(String prop, String fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  if (project.android.hasProperty("namespace")) {
    namespace 'com.arthenica.ffmpegkit.reactnative'
  }

  compileSdkVersion 33

  defaultConfig {
    minSdkVersion 24
    targetSdkVersion 33
    versionCode 602
    versionName "6.0.2"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }
}

repositories {
  mavenCentral()
  google()

  def found = false
  def defaultDir = null
  def androidSourcesName = 'React Native sources'

  if (rootProject.ext.has('reactNativeAndroidRoot')) {
    defaultDir = rootProject.ext.get('reactNativeAndroidRoot')
  } else {
    defaultDir = new File(
      projectDir,
      'node_modules/react-native/android'
    )
  }

  if (defaultDir.exists()) {
    maven {
      url defaultDir.toString()
      name androidSourcesName
    }

    logger.info(":${project.name}:reactNativeAndroidRoot ${defaultDir.canonicalPath}")
    found = true
  } else {
    def parentDir = rootProject.projectDir

    1.upto(5, {
      if (found) return true
      parentDir = parentDir.parentFile

      def androidSourcesDir = new File(
        parentDir,
        'node_modules/react-native'
      )

      def androidPrebuiltBinaryDir = new File(
        parentDir,
        'node_modules/react-native/android'
      )

      if (androidPrebuiltBinaryDir.exists()) {
        maven {
          url androidPrebuiltBinaryDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidPrebuiltBinaryDir.canonicalPath}")
        found = true
      } else if (androidSourcesDir.exists()) {
        maven {
          url androidSourcesDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidSourcesDir.canonicalPath}")
        found = true
      }
    })
  }

  if (!found) {
    throw new GradleException(
      "${project.name}: unable to locate React Native android sources. " +
        "Ensure you have you installed React Native as a dependency in your project and try again."
    )
  }
}

dependencies {
  api 'com.facebook.react:react-native:+'
  
  // Dynamically find any ffmpeg-kit AAR in the user's android/libs/ folder
  // Supports any variant: min, https, audio, video, full, full-gpl, etc.
  def ffmpegKitAarFound = false
  def libsDir = new File(rootProject.projectDir, '/libs')
  
  if (libsDir.exists()) {
    libsDir.eachFileMatch(~/ffmpeg-kit-.*\.aar/) { file ->
      def aarName = file.name.replaceAll('\\.aar$', '')
      implementation(name: aarName, ext: 'aar')
      logger.info("react-native-ffmpeg-kit: Using local AAR: ${aarName}")
      ffmpegKitAarFound = true
    }
  }
  
  // Fallback to Maven Central if no local AAR found
  if (!ffmpegKitAarFound) {
    def variant = project.hasProperty('ffmpegKitVariant') ? project.property('ffmpegKitVariant') : 'https'
    def version = project.hasProperty('ffmpegKitVersion') ? project.property('ffmpegKitVersion') : '6.0-2'
    implementation "com.arthenica:ffmpeg-kit-${variant}:${version}"
    logger.info("react-native-ffmpeg-kit: Using Maven Central: ffmpeg-kit-${variant}:${version}")
  }
}
